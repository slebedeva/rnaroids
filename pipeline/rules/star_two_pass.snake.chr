
""" rules for two pass mapping """

##def str_to_int(wildcards):
##  return str(wildcards)
## this is not used anywhere in the rule?

starPARS = """ \
  --outFilterMultimapNmax 10 --outFilterMismatchNmax 10 --outFilterMismatchNoverReadLmax 0.04 \
  --outSAMmultNmax 1 --outMultimapperOrder Random  --outSAMprimaryFlag AllBestScore \
  --alignIntronMin 20 --alignIntronMax 1000000 --alignMatesGapMax 1000000 \
  --alignSJoverhangMin 8  --alignSJDBoverhangMin 1 --sjdbScore 1 \
  --outFilterType BySJout \
  --outSAMtype BAM SortedByCoordinate --outSAMmode Full \
  --outSAMattributes All  --outSAMattrIHstart 0  --outSAMstrandField intronMotif  \
  --outWigType bedGraph  --outWigNorm RPM \
  --quantMode GeneCounts --readFilesCommand zcat \
  """
starPARS_dedup = """ \
  --outFilterMultimapNmax 10 --outFilterMismatchNmax 10 --outFilterMismatchNoverReadLmax 0.04 \
  --outSAMmultNmax 1 --outMultimapperOrder Random  --outSAMprimaryFlag AllBestScore \
  --alignIntronMin 20 --alignIntronMax 1000000 --alignMatesGapMax 1000000 \
  --alignSJoverhangMin 8  --alignSJDBoverhangMin 1 --sjdbScore 1 \
  --outReadsUnmapped Fastx --outFilterType BySJout \
  --outSAMtype BAM SortedByCoordinate --outSAMmode Full \
  --outSAMattributes All  --outSAMattrIHstart 0  --outSAMstrandField intronMotif  \
  """



rule star_second_pass:
   input:
     R1 = "{data}/fastq/dedup/{sample}_R1_{rep}.fastq.gz",
     R2 = "{data}/fastq/dedup/{sample}_R2_{rep}.fastq.gz",
     junctions = expand("{data}/star/firstpass/{sample}_{rep}_SJ.out.tab",
       data=DATA, sample=SAMPLE, rep=REPLICATE),
   output:
     bam = "{data}/star/secondpass/{sample}_{rep}_2pass.bam",
     bg1 = "{data}/star/secondpass/{sample}_{rep}_2pass_Signal.Unique.str1.out.bg",
     bg2 = "{data}/star/secondpass/{sample}_{rep}_2pass_Signal.Unique.str2.out.bg"
   params:
     job_name = "star_pass_2",
     tmpbam = "{data}/star/secondpass/{sample}_{rep}_2pass_Aligned.sortedByCoord.out.bam",
     out = "{data}/star/secondpass/{sample}_{rep}_2pass_",
   log:
     "{data}/star/logs/secondpass/{sample}_{rep}_align.txt"
   message:
     "running second pass star alignments "
   threads:
     4
   resources: all_threads=4
   shell:
     """
     STAR \
       --genomeDir {GENOME_DIR}  \
       --runThreadN {threads} \
       --outBAMsortingThreadN {threads} \
       --readFilesIn {input.R1} {input.R2} \
       --readFilesCommand gunzip -c \
       --outFileNamePrefix {params.out} \
       --sjdbFileChrStartEnd {input.junctions} \
       --outFilterMultimapNmax 10 --outFilterMismatchNmax 10 --outFilterMismatchNoverReadLmax 0.04 \
       --outSAMmultNmax 1 --outMultimapperOrder Random  --outSAMprimaryFlag AllBestScore \
       --alignIntronMin 20 --alignIntronMax 1000000 --alignMatesGapMax 1000000 \
       --alignSJoverhangMin 8  --alignSJDBoverhangMin 1 --sjdbScore 1 \
       --outReadsUnmapped Fastx --outFilterType BySJout \
       --outSAMtype BAM SortedByCoordinate --outSAMmode Full \
       --limitSjdbInsertNsj=1500000 \
       --outSAMattributes All  --outSAMattrIHstart 0  --outSAMstrandField intronMotif \
       --outWigType bedGraph  --outWigNorm RPM 

     # add XS tag for non-spliced alignments using awkful script from STAR 
     samtools view -h {params.tmpbam} \
       | awk -v strType=2 -f {SRC}/tagXSstrandedData.awk \
       | samtools view -bS - > {output.bam}
     
     samtools index {output.bam}
     
     rm -f {params.tmpbam}
     """


rule extract_deduped_fastqs:
    """
    convert reads back to fastq for second pass mapping
    read ids will have /1 and /2 appended
    samtools 1.5
    """
    input:
      bam = "{data}/star/dedup/dedup_{sample}_{rep}.bam",
    output:
      R1 = "{data}/fastq/dedup/{sample}_R1_{rep}.fastq.gz",
      R2 = "{data}/fastq/dedup/{sample}_R2_{rep}.fastq.gz",
      singles = "{data}/fastq/dedup/{sample}_{rep}_singletons.fastq.gz",
    params:
      fq = "{data}/fastq/dedup/{sample}_{rep}.fastq",
      job_name = "makefq.{sample}_{rep}",
    log:
      "{data}/fastq/logs/dedup/{sample}_{rep}_makefastq.txt"
    message:
      "converting deduplicated bam back to fastq"
    resources: all_threads=4
    threads: 4
    shell:
      """
        samtools sort -n {input.bam} |
        samtools fastq \
        -0 {output.singles} \
        -1 {output.R1} \
        -2 {output.R2} \
        -@ 3 -
      """


def aggregate_chrom_bams(wildcards):
    out = expand("{data}/star/dedup/{sample}_{rep}/dedup_split_{chrom}_{sample}_{rep}.bam", 
                   chrom = CHROMS, 
                   data = wildcards.data, 
                   sample = wildcards.sample,
                   rep = wildcards.rep)
    return(out)

rule combine_bams:
    """
    combined deduplicated chromosome bams
    samtools cat 
    """
    input:
      bam = aggregate_chrom_bams, 
    output:
      bam = "{data}/star/dedup/dedup_{sample}_{rep}.bam", 
    params:
      job_name = "dedup.combine.{sample}_{rep}",
    log:
      "{data}/star/logs/dedup/{sample}_{rep}_dedup_combine.txt"
    resources: all_threads=1
    message:
      "combining per chromosome {wildcards.sample} bam files"
    shell:
      """
      # combine and sort by read name so pairs are together for conversion back to fastq
      samtools cat {input.bam} \
        | samtools sort -n -o {output.bam}
      """

rule dedup_bam:
    """
    deduplicate reads
    umi_tools version 4.4
    use python3/3.6.1
    """
    input:
      bam = "{data}/star/firstpass/{sample}_{rep}.bam",
    output:
      #bam = "{data}/star/dedup/dedup_{sample}_{rep}.bam",
      bam = "{data}/star/dedup/{sample}_{rep}/dedup_split_{chrom}_{sample}_{rep}.bam",
    params:
      job_name = "dedup_bams",
    log:
      "{data}/star/logs/dedup/{sample}_{rep}_dedup.txt"
    resources: all_threads=4
    shell:
      """
      # use python3 - needed for umi_tools 
      # use updated v4.4
      
      umi_tools dedup \
        --method="directional" \
        --paired \
        --chrom={wildcards.chrom} \
        -v 1 \
        -L {log} \
        -I {input.bam} \
        -S {output.bam}

      """


rule star_first_pass:
    """ map reads to genome/transcriptome using STAR 
    STAR_2.5.1b
    """
    input:
      R1 = "{data}/fastq/filtered/{sample}_R1_{rep}_umi_trim_noercc.fastq.gz",
      R2 = "{data}/fastq/filtered/{sample}_R2_{rep}_umi_trim_noercc.fastq.gz",
      genome = GENOME_DIR + "/Genome"
    output:
      "{data}/star/firstpass/{sample}_{rep}_SJ.out.tab",
      bam = temp("{data}/star/firstpass/{sample}_{rep}.bam"),
    params:
      job_name = "star_pass_1",
      tmpbam = "{data}/star/firstpass/{sample}_{rep}_Aligned.sortedByCoord.out.bam",
      out = "{data}/star/firstpass/{sample}_{rep}_",
    log:
      "{data}/star/logs/firstpass/{sample}_{rep}_align.txt"
    message:
      "running first pass star alignments "
    threads:
      4
    resources: all_threads=4
    shell:
      """
      STAR \
        --genomeDir {GENOME_DIR}  \
        --runThreadN {threads} \
        --readFilesIn {input.R1} {input.R2} \
        --outBAMsortingThreadN {threads} \
        --outFileNamePrefix {params.out} \
        --outFilterMultimapNmax 10 --outFilterMismatchNmax 10 --outFilterMismatchNoverReadLmax 0.04 \
        --outSAMmultNmax 1 --outMultimapperOrder Random  --outSAMprimaryFlag AllBestScore \
        --alignIntronMin 20 --alignIntronMax 1000000 --alignMatesGapMax 1000000 \
        --alignSJoverhangMin 8  --alignSJDBoverhangMin 1 --sjdbScore 1 \
        --outReadsUnmapped Fastx --outFilterType BySJout \
        --outSAMtype BAM SortedByCoordinate --outSAMmode Full \
        --outSAMattributes All  --outSAMattrIHstart 0  --outSAMstrandField intronMotif \
        --outWigType bedGraph  --outWigNorm RPM \
        --quantMode GeneCounts --readFilesCommand zcat 
      
      # add XS tag for non-spliced alignments using awkful script from STAR 
      samtools view -h {params.tmpbam} \
        | awk -v strType=2 -f {SRC}/tagXSstrandedData.awk \
        | samtools view -bS - > {output.bam}
      
      samtools index {output.bam}
      
      #rm -f {params.tmpbam}
      """
